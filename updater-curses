#! /usr/bin/perl -w
use strict;
use Getopt::Std;
use FAI::Updater;
use FAI::Updater::Display::Logfile;
use FAI::Updater::Display::Curses;
use POSIX qw(strftime);
$Getopt::Std::STANDARD_HELP_VERSION=1;

# options
our ($opt_H,$opt_s,$opt_n,$opt_o);

our $VERSION="0.1";

my $netgroup='hand-defined';
my $cui;
my $updater;
my $statusdisplay;
my $quit=0;

sub HELP_MESSAGE {
  my $FH = shift;
  print $FH <<EOF
  
Usage: $0 [options] <netgroup|-H host1,host2,...>
  --help            display this help message
  --version         print version
  -o                ordered mode: don't randomize order of hosts
  -s <number>       number of updates running simultanously
  -n                dryrun mode: use a dummy-script instead of really 
                    contacting the clients
EOF
}

sub updater_cb() {
	if ($updater->run()) {
	} elsif ($quit) {
		exit(0);
	} else {
		$statusdisplay->text("idle");
		$statusdisplay->draw();
	}
}

sub quit() {
	shutdown_update();
	$quit=1;
}

sub shutdown_update() {
	if ($updater->max_simultanous() > 0) {
		$statusdisplay->text("shutting down...");
		$statusdisplay->draw();
		$updater->max_simultanous(0);
	}
}
# - - - - - - - - - - - - - - - - - - - -
#  main routine
# - - - - - - - - - - - - - - - - - - - -

getopts('hH:s:no');
# emit help message when no hosts/no netgroup is given on the command line
(($#ARGV<0) and ! $opt_H) and HELP_MESSAGE(*STDERR) and exit(1);
$netgroup=$ARGV[0] if $ARGV[0];

#initialize Curses
$cui=new Curses::UI(-color_support=>1, -clear_on_exit=>1);
#create window for the updater display, leave space for the statusbar
my $updater_win=$cui->add('UpdateColumns','Window',-height=>$cui->height()-1);
#create the updater display
my $display=FAI::Updater::Display::Curses->new(WIN=>$updater_win);

# build up the statusbar. as the updater display is now correctly layouted, 
# use its width for proper alignment
my $statusbar=$cui->add('Statusbar','Window', 
	-y=>$cui->height()-1,
	-width=>$updater_win->width());
# hotkey descriptions
$statusbar->add(undef,'Label',-text=>'q:Quit s:Shutdown');
# Status
$statusbar->add(undef,'Label',-text=>'Status: ',
	-x=>-21);
$statusdisplay=$statusbar->add('statusdisplay','Label',
	-text=>'processing',
	-x=>-1,
	-width=>20,
	-paddingspaces=>1,
	-textalignment=>'middle',
	-sbborder=>1,
	-bg=>'red');
	
$updater_win->focus();
$cui->set_binding( \&quit, "q");
$cui->set_binding( \&shutdown_update, "s");

# create logdir
my $logdir="/var/log/fai-updater/".strftime("%Y-%m-%d_%H-%M-%S",localtime);
die "can't create logdir $logdir" unless mkdir $logdir;
$display->append(FAI::Updater::Display::Logfile->new(FILENAME=>"$logdir/FAI_UPDATER.log"));
$updater=FAI::Updater->new(DISPLAY=>$display,PING=>0,DRYRUN=>$opt_n,ORDERED=>$opt_o,LOGDIR=>$logdir);
$updater->max_simultanous($opt_s) if $opt_s;
$updater->init_hostlist( $opt_H ? split(/,/,$opt_H) : split(/\s/,`libexec/get_hostlist $netgroup`));

$cui->draw();
$cui->set_timer('updater',\&updater_cb);
$cui->mainloop;
